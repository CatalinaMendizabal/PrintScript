plugins {
  // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
  id 'org.jetbrains.kotlin.jvm'
  id 'java-library'
  id 'linter.common'
  id 'maven-publish'
  id 'jacoco'
}

group 'com.g3.austral.ingsis.printscript'

repositories {
  mavenCentral()
  maven {
    url = uri("https://maven.pkg.github.com/austral-ingsis/printscript-parser-common")
    credentials {
      username = System.getenv("GITHUB_ACTOR")
      password = System.getenv("GITHUB_TOKEN")
    }
  }
}

dependencies {
  implementation "org.austral.ingsis.printscript:printscript-parser-common:0.1.1"
  testImplementation "org.jetbrains.kotlin:kotlin-test"
}

tasks.named('test') {
  useJUnitPlatform()
  finalizedBy jacocoTestReport // report is always generated after tests run
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/CatalinaMendizabal/PrintScript")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components["java"])
        }
    }
}

jacoco {
  toolVersion = "0.8.8"
  reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = false
    csv.required = false
    html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
  }
}
